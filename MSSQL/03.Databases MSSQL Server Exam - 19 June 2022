-- 00. Create DB

CREATE DATABASE Zoo

-- 01. DDL

CREATE TABLE Owners
(
	Id INT PRIMARY KEY IDENTITY
   ,[Name] VARCHAR(50) NOT NULL
   ,PhoneNumber VARCHAR(15) NOT NULL
   ,[Address] VARCHAR(50) NULL
)

CREATE TABLE AnimalTypes
(
	Id INT PRIMARY KEY IDENTITY
   ,AnimalType VARCHAR(30) NOT NULL
)

CREATE TABLE Cages
(
	Id INT PRIMARY KEY IDENTITY
   ,AnimalTypeId INT FOREIGN KEY REFERENCES AnimalTypes(Id) NOT NULL
)

CREATE TABLE Animals
(
	Id INT PRIMARY KEY IDENTITY
   ,[Name] VARCHAR(30) NOT NULL
   ,BirthDate DATE NOT NULL
   ,OwnerId INT FOREIGN KEY REFERENCES Owners(Id) NULL
   ,AnimalTypeId INT FOREIGN KEY REFERENCES AnimalTypes(Id) NOT NULL
)

CREATE TABLE AnimalsCages
(
	CageId INT NOT NULL
   ,AnimalId INT NOT NULL
   ,PRIMARY KEY (CageId, AnimalId)
   ,FOREIGN KEY (CageId) REFERENCES Cages(Id) 
   ,FOREIGN KEY (AnimalId) REFERENCES Animals(Id) 
)	

CREATE TABLE VolunteersDepartments
(
	Id INT PRIMARY KEY IDENTITY
   ,DepartmentName VARCHAR(30) NOT NULL
)	

CREATE TABLE Volunteers
(
	Id INT PRIMARY KEY IDENTITY
   ,[Name] VARCHAR(50) NOT NULL
   ,PhoneNumber VARCHAR(15) NOT NULL
   ,[Address] VARCHAR(50) NULL
   ,AnimalId INT FOREIGN KEY REFERENCES Animals(Id) NULL
   ,DepartmentId INT FOREIGN KEY REFERENCES VolunteersDepartments(Id) NOT NULL
)

-- 02. Insert

INSERT INTO Volunteers([Name], PhoneNumber, [Address], AnimalId, DepartmentId)
VALUES
	('Anita Kostova', '0896365412', 'Sofia, 5 Rosa str.', 15, 1)
   ,('Dimitur Stoev', '0877564223', NULL, 42, 4)
   ,('Kalina Evtimova', '0896321112', 'Silistra, 21 Breza str.', 9, 7)
   ,('Stoyan Tomov', '0898564100', 'Montana, 1 Bor str.', 18, 8)
   ,('Boryana Mileva', '0888112233', NULL, 31, 5)

INSERT INTO Animals([Name],BirthDate,OwnerId,AnimalTypeId)
VALUES
	('Giraffe', '2018-09-21', 21, 1)
   ,('Harpy Eagle', '2015-04-17', 15, 3)
   ,('Hamadryas Baboon', '2017-11-02', NULL, 1)
   ,('Tuatara', '2021-06-30', 2, 4)

-- 03. Update

UPDATE Animals
SET OwnerId = 4
WHERE OwnerId IS NULL

-- 04. Delete

DELETE Volunteers
WHERE DepartmentId IN (2)
DELETE VolunteersDepartments
WHERE DepartmentName = 'Education program assistant'

-- 05. Volunteers

SELECT 
	v.[Name]
   ,v.PhoneNumber
   ,v.[Address]
   ,v.AnimalId
   ,v.DepartmentId
FROM Volunteers AS v
ORDER BY v.[Name],v.AnimalId,v.DepartmentId DESC

-- 06. Animals data

SELECT 
	a.[Name]
   ,t.AnimalType
   ,FORMAT(a.BirthDate, 'dd.MM.yyyy') AS BirthDate
FROM Animals AS a
JOIN AnimalTypes AS t ON a.AnimalTypeId = t.Id
ORDER BY a.[Name]

-- 07. Owners and Their Animals

SELECT TOP(5)
	o.[Name]
   ,COUNT(a.OwnerId) AS CountOfAnimals
FROM Animals AS a
JOIN Owners AS o ON a.OwnerId = o.Id
GROUP BY o.[Name]
ORDER BY CountOfAnimals DESC, o.[Name]

-- 08. Owners, Animals and Cages

SELECT
	CONCAT(o.[Name], '-',a.[Name]) AS OwnersAnimals
   ,o.PhoneNumber
   ,ac.CageId AS CageId
FROM AnimalsCages AS ac
JOIN Cages AS c ON ac.CageId = c.Id
JOIN Animals AS a ON ac.AnimalId = a.Id
JOIN Owners AS o ON a.OwnerId = o.Id
WHERE a.AnimalTypeId = 1
ORDER BY o.[Name], a.[Name] DESC

-- 09. Volunteers in Sofia

SELECT
	v.[Name]
   ,v.PhoneNumber
   ,SUBSTRING(v.[Address],
	CHARINDEX(',', v.[Address]) + 1,
	LEN(v.[Address]) - CHARINDEX(',', v.[Address])) 
FROM Volunteers AS v
JOIN VolunteersDepartments AS vd ON v.DepartmentId = vd.Id
WHERE DepartmentName = 'Education program assistant' 
AND v.[Address] LIKE '%Sofia%'
ORDER BY v.[Name]


-- 10. Animals for Adoption

SELECT 
	a.[Name]  
   ,YEAR(a.BirthDate) BirthYear
   ,[at].AnimalType
FROM Animals AS a
JOIN AnimalTypes AS [at] ON a.AnimalTypeId = [at].Id
WHERE a.OwnerId IS NULL
AND YEAR(a.BirthDate) > 2017
AND [at].AnimalType != 'Birds'
ORDER BY a.[Name]

-- 11. All Volunteers in a Department

GO

CREATE FUNCTION udf_GetVolunteersCountFromADepartment (@VolunteersDepartment VARCHAR(30)) 
RETURNS INT
AS
BEGIN
	DECLARE @count INT
	SET @count = (SELECT 
					  COUNT(*) 
				  FROM Volunteers AS v
				  JOIN VolunteersDepartments AS vd ON v.DepartmentId = vd.Id
				  WHERE vd.DepartmentName = @VolunteersDepartment
				  GROUP BY  vd.DepartmentName)	

	RETURN @count
END

-- 12. Animals with Owner or Not
GO

CREATE PROC usp_AnimalsWithOwnersOrNot(@AnimalName VARCHAR(30))
AS
BEGIN
	SELECT 
		a.[Name]
	   ,ISNULL(o.[Name], 'For adoption')
	FROM Animals AS a
	LEFT JOIN Owners AS o ON a.OwnerId = o.Id
	WHERE a.[Name] = @AnimalName
END
